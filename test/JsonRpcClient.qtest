#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%require-types
%strict-args
%enable-all-warnings

%requires ../qlib/JsonRpcConnection.qm
%requires QUnit

%exec-class JsonRpcClientTest

class JsonRpcClientTest inherits QUnit::Test {
    constructor() : Test("JsonRpcClient test", "1.0") {
        addTestCase("JSON-RPC client test", \testJsonRpcClient());
        addTestCase("JSON-RPC connection test", \testJsonRpcConnection());

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    testJsonRpcClient() {
        JsonRpcClient client(("url": "http://localhost:80"), True);

        assertEq(True, client instanceof JsonRpcClient);
    }

    testJsonRpcConnection() {
        string url = "jsonrpcs://localhost:8099/";
        JsonRpcConnection conn("test", "test", url, False, NOTHING, parse_url(url));
        JsonRpcClient client = conn.get(False);
        string yurl = url;
        yurl =~ s/^jsonrpc/http/;
        assertEq(yurl, client.getURL());

        # test default path
        url = "jsonrpcs://localhost:8099";
        conn = new JsonRpcConnection("test", "test", url, False, NOTHING, parse_url(url));
        client = conn.get(False);
        yurl = url;
        yurl =~ s/^jsonrpc/http/;
        assertEq(yurl + "/JSON", client.getURL());

        hash<ConnectionConstructorInfo> info = conn.getConstructorInfo();
        assertEq("json", info.module);
        assertEq("Qore::Json::JsonRpcClient", info.class_name);
        assertEq("https://localhost:8099", info.args[0].url);
        checkPreAndPostProcessing(info);
    }

    private checkPreAndPostProcessing(hash<ConnectionConstructorInfo> info) {
        Program p(PO_NEW_STYLE | PO_STRICT_ARGS | PO_REQUIRE_TYPES);
        if (info.module)
            p.loadModule(info.module);
        if (info.pre_processing) {
            string pre_processing = sprintf("sub pre_processing(reference<*softlist<auto>> args, bool connect, *hash rtopts) { %s }", info.pre_processing);
            # ensure that the pre processing code can be parsed
            p.parse(pre_processing, "pre processing");
            assertTrue(True, "pre processing: " + info.class_name);
        }
        if (info.post_processing) {
            string post_processing = sprintf("sub post_processing(%s obj, bool connect, *hash rtopts) { %s }", info.class_name, info.post_processing);
            # ensure that the post processing code can be parsed
            p.parse(post_processing, "post processing");
            assertTrue(True, "post processing: " + info.class_name);
        }
    }
}
